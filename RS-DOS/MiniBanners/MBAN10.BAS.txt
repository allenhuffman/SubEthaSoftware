0 REM *
1 REM * MiniBanners 1.0 by Allen Huffman
2 REM *        Copyright (C) 1990
3 REM *       by Sub-Etha Software
4 REM *
5 CLEAR1500:DIMLN(15),HT(15):DR=PEEK(&H95A):DV=-2:VR$="1.0":SN$="00042"
6 ONBRK GOTO705:ONERR GOTO730:POKE65497,0
7 FG=63:BG=0:CH=219:BD=41:PW=80:SP=0:WD=1:FT$="Default"
10 PALETTE0,BG:PALETTE8,FG:WIDTH40:CLS1:PRINTTAB(11)"/) MiniBanners! (\":ATTR2,4:PRINT:ATTR0,0:LOCATE0,22:ATTR2,4:PRINT:ATTR0,0:LOCATE10,3:PRINT"Release Version: "VR$
15 PRINT:PRINT" Copyright (C) 1990 by Allen C. Huffman":PRINT:PRINTTAB(18)"and":PRINT:PRINTTAB(11)"Sub-Etha Software":PRINTTAB(12)"P.O. Box 152442":PRINTTAB(10)"Lufkin, Texas 75915":PRINTTAB(9)"(409) 639-ETHA [3842]"
20 PRINT:PRINTTAB(5)"Support the future of the CoCo.":PRINT"   Please do not pirate this program."
25 LOCATE10,18:ATTR2,4:PRINT"Serial Number # "SN$;:ATTR0,0
30 LOCATE6,23:PRINT"Load Config? [Y]es or [N]o :";:GOSUB1055:IFA$<>"Y"THEN55
35 LOCATE8,20:ATTR0,0,B:PRINT"Checking Configuration...";:ATTR0,0:GOSUB755
50 REM * Main Menu
55 CLS:PRINTTAB(9)"/) MiniBanners! "VR$" (\":ATTR2,4:PRINT:ATTR0,0:LOCATE0,22:ATTR2,4:PRINT:ATTR0,0:PRINTTAB(13)"Enter Function";
60 LOCATE11,6:PRINT"[1] Print a Banner":PRINT:PRINTTAB(11)"[2] Select Font ("FT$")":PRINT:PRINTTAB(11)"[3] Configuration"
65 LOCATE11,19:PRINT"[X] Exit to Basic"
70 LOCATE15,3:PRINT"[D]rive:"DR:DRIVEDR
75 GOSUB1055:A=INSTR("123DX",A$):IFA=0THENSOUND200,1:GOTO75
80 ONA GOTO105,205,305,85,95
85 LOCATE24,3:PRINT"?";:GOSUB1055:A=INSTR("0123",A$):IFA$=CHR$(13)ORA=0THEN70ELSEDR=A-1:GOTO70
95 CLS:PRINT"    Thank You for using MiniBanners!":POKE65496,0:POKE150,18:NEW
100 REM * Print a Banner
105 CLS:PRINTTAB(10)"/) Print a Banner (\":ATTR2,4:PRINT:ATTR0,0:POKE150,BD
110 LOCATE9,2:PRINTUSING"Printer Width [###] :";PW;:GOSUB1005:A=VAL(A$):IFA=0THENA=PW
111 IFA<32ORA>132THEN110ELSEPW=A
112 LOCATE9,3:PRINTUSING"Font Width    [###] :";WD;:GOSUB1005:A=VAL(A$):IFA>0ANDA<20THENWD=A
113 LOCATE9,4:PRINTUSING"Line Spacing  [###] :";SP;:GOSUB1005:IFA$=""THEN114ELSEA=VAL(A$):IFA=>0ANDA<20THENSP=A
114 LOCATE9,5:PRINTUSING"Ptr Character [###] :";CH;:GOSUB1005:IFA$=""THEN115ELSECH=VAL(A$)
115 B=0:CT=0:HT=INT(PW/(8+SP)):LN=0:MX=0:LOCATE0,2:PRINT:PRINT:PRINT:PRINT:ATTR2,4:PRINT"Ln Ht Text":ATTR0,0
120 LOCATE0,3:PRINT:LOCATE0,3:PRINT">";:GOSUB1005:IFA$=""THENIFLN=0THEN55ELSELN=LN-1:GOTO140
121 IFA$="^"THENA$=CHR$(127)
125 LN$(LN)=A$:IFLEN(A$)>MX THENMX=LEN(A$)
130 LOCATE11,4:PRINTUSING"Height [##] Max  :";HT;:GOSUB1005:A=VAL(A$):IFA=0THENA=HT
131 IFA<1ORA>HT THEN130ELSEHT(LN)=A:HT=HT-A:B=B+A*8+SP
135 LOCATE0,7+LN:PRINTUSING"## ";LN+1;HT(LN);:PRINTLEFT$(LN$(LN),33):IFHT>0THENLN=LN+1:GOTO120
140 CT=(PW-B)/2+SP/2:IFHT>0THENLOCATE6,23:PRINT"Center from Top to Bottom?";:GOSUB1055:IFA$<>"Y"THENCT=0
150 FORA=0TOLN:A$=STRING$(MX,32):IFLEN(LN$(A))<>MX THENMID$(A$,(MX-LEN(LN$(A)))/2+1,LEN(LN$(A)))=LN$(A):LN$(A)=A$
151 NEXT
155 LOCATE6,23:PRINT"PRINT - Is Everything Okay?";:GOSUB1055:IFA$<>"Y"THEN105
160 LOCATE6,23:PRINT"PRINTING - [BREAK] to Quit:";:PRINT#DV:PRINT#DV,TAB(PW/2-15)"/) MiniBanners "VR$" SN#"SN$" (\"
165 PRINT#DV:FORA=1TOMX:FORB=7TO0STEP-1:FORW=1TOWD:PRINT#DV,STRING$(CT,32);:FORC=LN TO0STEP-1:LT=ASC(MID$(LN$(C),A,1))-32:FORD=7TO0STEP-1
170 IF(PEEK(&HF09D+LT*8+D)AND2^B)>0THENPRINT#DV,STRING$(HT(C),CH);ELSEPRINT#DV,STRING$(HT(C),32);
175 NEXT:IFLN>0ANDC>0THENPRINT#DV,STRING$(SP,32);
180 NEXT:PRINT#DV:NEXT:NEXT:NEXT:GOTO55
200 REM * Select Font
205 CLS:PRINTTAB(12)"/) Select Font (\":ATTR2,4:PRINT:ATTR0,0:LOCATE0,22:ATTR2,4:PRINT:ATTR0,0
210 EX$="FNT":GOSUB930:IFA$=""THEN55
215 GOSUB840:IFA=0THEN210ELSEPOKE65496,0:LOADMA$+"."+EX$:POKE65497,0:FT$=A$
220 GOTO55
300 REM * Configuration
305 CLS:PRINTTAB(10)"/) Configuration (\":ATTR2,4:PRINT:ATTR0,0:PRINT:LOCATE0,22:ATTR2,4:PRINT:ATTR0,0
310 LOCATE11,3:PRINT"Current Settings:":PRINT:PRINT"Printer Width:"PW:PRINT:PRINT"Printer Baud :"BD:PRINT:PRINT"Font Width   :"WD:PRINT:PRINT"Line Spacing :"SP:PRINT:PRINT"Banner Char. :"CH:PRINT:PRINT"Default Font : "FT$
315 LOCATE1,23:PRINT"[E]dit [L]oad [S]ave or [X]it Config:";:GOSUB1055:A=INSTR("ELSX",A$):IFA=0THEN315ELSEIFA=4THEN55
320 IFA=2THENGOSUB755:GOTO305ELSEIFA=3THENGOSUB780:GOTO305
325 LOCATE2,20:PRINT"Enter your printer's carraige width.":LOCATE15,5:GOSUB1005:A=VAL(A$):IFA>32ANDA<132THENPW=A
330 LOCATE14,5:PRINTPW:LOCATE0,20:PRINT"Baud*2: 600=180, 1200=88, 2400=41, etc.":LOCATE15,7:GOSUB1005:A=VAL(A$):IFA>0THENBD=A
335 LOCATE14,7:PRINTBD:LOCATE0,20:PRINT" Enter default font width for banners.":LOCATE15,9:GOSUB1005:A=VAL(A$):IFA>0ANDA<10THENWD=A
340 LOCATE14,9:PRINTWD:LOCATE0,20:PRINT"  Enter spacing between banner lines.":LOCATE15,11:GOSUB1005:IFA$=""THEN345ELSEIFA=>0ANDA<20THENSP=A
345 LOCATE14,11:PRINTSP:LOCATE0,20:PRINT"  Enter ASCII value of char. to print.":LOCATE15,13:GOSUB1005:A=VAL(A$):IFA>32THENCH=A
350 LOCATE14,13:PRINTCH:LOCATE0,20:PRINT"   Enter font name to load on startup.":LOCATE15,15:GOSUB1005:IFA$<>""THENFT$=LEFT$(A$,8)
355 GOTO305
500 PRINT"[END]";
501 GOTO501
700 REM * Break Detected
705 CLS:PRINTTAB(10)"/) Break Detected (\":ATTR2,4:PRINT:ATTR0,0
710 LOCATE4,13:PRINT"[ENTER] to Restart, [X] to Exit:";:GOSUB1055:IFA$="*"THENSTOP
715 IFA$="X"THEN95ELSERUN
720 END
725 REM * Error Detected :(
730 CLS:PRINTTAB(10)"/) Error Detected (\":ATTR2,4:PRINT:ATTR0,0
735 LOCATE7,4:PRINT"[ Error"ERNO"reported at"ERLIN"]":PRINT:PRINT" Honestly, I thought I had it bug-free!":PRINT:PRINT"   If this was something other than a":PRINT"   Disk Full or I/O Error, please let":PRINT"   us know so we may eliminate it..."
740 GOTO710
750 REM * Load Configuration
755 A$="MINIBAN":EX$="CFG":GOSUB840:IFA=0THENRETURN
760 POKE65496,0:OPEN"I",#1,"MINIBAN.CFG":INPUT#1,CH,BD,PW,SP,WD,FT$:CLOSE:POKE65497,0
765 A$=FT$:EX$="FNT":GOSUB840:IFA=0THENRETURNELSELOADMA$+"."+EX$
770 RETURN
775 REM * Save Configuration
780 POKE65496,0:OPEN"O",#1,"MINIBAN.CFG":WRITE#1,CH,BD,PW,SP,WD,FT$:CLOSE:POKE65497,0:RETURN
800 REM * Directory Subroutine
805 GOSUB855:C=0:FORA=3TO11:POKE65496,0:DSKI$DR,17,A,A$,B$:POKE65497,0:C$=A$+LEFT$(B$,127):FORB=1TO255STEP32:IFEX$>""THENIFMID$(C$,B+8,3)<>EX$THEN820
810 D=ASC(MID$(C$,B,1)):IFD=0THEN815ELSEIFD=255THENRETURNELSEC=C+1:A$=MID$(C$,B,8):PRINT"    "A$"."MID$(C$,B+8,3)"    ";
815 D=20+(SGN(LEN(EX$))*10):IFC/D=INT(C/D)THEN825
820 NEXT:NEXT:RETURN
825 LOCATE2,23:PRINT"Press [ENTER] for More, [X] to Stop:";:GOSUB1055:LOCATE0,23:PRINTSTRING$(39,32);:IFA$="X"THENRETURN
830 GOSUB855:C=0:GOTO820
835 REM * Verify File Name
840 A=0:FORB=3TO11:POKE65496,0:DSKI$DR,17,B,C$,D$:POKE65497,0:D$=C$+LEFT$(D$,127):FORC=1TO255STEP32:IFMID$(D$,C,8)+"."+MID$(D$,C+8,3)=A$+STRING$(8-LEN(A$),32)+"."+EX$ THEN845 ELSENEXT:NEXT:RETURN
845 A=1:RETURN
850 REM * Clear Screen Sub
855 FORC=0TO15:LOCATE0,5+C:PRINT:NEXT:LOCATE0,6:RETURN
870 NEXT:NEXT:RETURN
925 REM * Load from Disk...
930 LOCATE5,3:PRINT"Enter Font Name, [D]irectory,":PRINTTAB(5)"or [P]review: ........":LOCATE19,4:GOSUB1005:IFA$=""THENRETURNELSEIFLEN(A$)>8THEN930
935 IFA$="D"THENGOSUB805:GOTO930 ELSEIFA$="P"THENGOSUB950:GOTO930
940 RETURN
945 REM * Font Preview
950 HSCREEN2:HCOLOR8:LN$="        ":FORA=32TO127:LN$=LN$+CHR$(A):NEXT
955 C=0:FORA=3TO11:POKE65496,0:DSKI$DR,17,A,A$,B$:POKE65497,0:C$=A$+LEFT$(B$,127):FORB=1TO255STEP32:IFMID$(C$,B+8,3)<>"FNT"THEN970ELSED=ASC(MID$(C$,B,1)):IFD=0THEN965ELSEIFD=255THENRETURNELSEA$=MID$(C$,B,8):LOADMA$+".FNT":MID$(LN$,1,8)=A$
960 HPRINT(0,C*4),LEFT$(LN$,40):HPRINT(0,C*4+1),MID$(LN$,40,40):HPRINT(0,C*4+2),MID$(LN$,80):C=C+1
965 IFC/6=INT(C/6)THENGOSUB1055:IFA$="X"THENHSCREEN0:RETURNELSEHCLS:C=0
970 NEXT:NEXT:GOSUB1055:HSCREEN0:RETURN
975 REM * Press Enter...
980 LOCATE7,23:PRINT"Press [ENTER] to Continue:";
985 IFINKEY$<>CHR$(13)THEN985ELSERETURN
998 PRINT:PRINT"*ERROR* "ERNO;ERLIN
999 WIDTH80:POKE65496,0:POKE150,18:END
1000 REM * Line Input
1005 LINEINPUTA$:RETURN
1050 REM * Inkey$
1055 A$=INKEY$:IFA$=""THEN1055ELSESOUND1,1:LT=ASC(A$):IFLT>96THENA$=CHR$(LT-32)
1060 RETURN
5000 '
5001 'V0.0 - Program Began 9/20/90
5002 'V1.0 - CoCo Fest Release 10/90
