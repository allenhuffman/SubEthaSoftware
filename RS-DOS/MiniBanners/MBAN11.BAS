0 REM *
1 REM * MiniBanners 1.1 by Allen Huffman
2 REM *        Copyright (C) 1990
3 REM *       by Sub-Etha Software
4 REM *
5 CLEAR1500:DIMLN$(15),HT(15):DR=PEEK(&H95A):DV=-2:VR$="1.1":SN$="00042"
6 ONBRK GOTO705:ONERR GOTO730:POKE65497,0
7 FG=63:BG=0:CH=219:BD=2400:PW=80:SP=0:SW=40:WD=1:DF$="DEFAULT":FT$="Current"
8 IF(PEEK(338)AND64)=0THENSW=80
9 TB=(SW/40-1)*20:PALETTE1,FG:PALETTE9,BG:POKE63395,9:POKE63468,9:POKE63503,0:POKE63567,0:POKE63644,0:POKE63771,0
10 PALETTE0,BG:PALETTE8,BG:WIDTHSW:CLS1:PRINTTAB(TB+11)"/) MiniBanners! (\":ATTR1,1:PRINT:ATTR0,0:LOCATE0,22:ATTR1,1:PRINT:ATTR0,0:LOCATETB+10,3:PRINT"Release Version: "VR$
15 PRINT:PRINTTAB(TB)" Copyright (C) 1990 by Allen C. Huffman":PRINT:PRINTTAB(TB+18)"and":PRINT:PRINTTAB(TB+11)"Sub-Etha Software":PRINTTAB(TB+12)"P.O. Box 152442":PRINTTAB(TB+10)"Lufkin, Texas 75915":PRINTTAB(TB+9)"(409) 639-ETHA [3842]"
20 PRINT:PRINTTAB(TB+5)"Support the future of the CoCo.":PRINTTAB(TB+3)"Please do not pirate this program."
25 LOCATETB+9,18:ATTR1,1:PRINT"Serial Number # "SN$;:ATTR0,0:PALETTE8,FG
30 LOCATETB+6,23:PRINT"Load Config? [Y]es or [N]o :";:GOSUB1055:IFA$<>"Y"THEN40
35 LOCATETB+7,20:ATTR0,0,B:PRINT"Checking Configuration...";:ATTR0,0:GOSUB755
40 GOSUB880
50 REM * Main Menu
55 PALETTE8,BG:CLS:PRINTTAB(TB+9)"/) MiniBanners! "VR$" (\":ATTR1,1:PRINT:ATTR0,0:LOCATE0,22:ATTR1,1:PRINT:ATTR0,0:PRINTTAB(TB+13)"Enter Function";
60 LOCATETB+11,6:PRINT"[1] Print a Banner":PRINT:PRINTTAB(TB+11)"[2] Select Font ("FT$")":PRINT:PRINTTAB(TB+11)"[3] Configuration"
65 LOCATETB+11,17:PRINT"[W] Toggle 40/80":LOCATETB+11,19:PRINT"[X] Exit to Basic"
70 LOCATETB+15,3:PRINT"[D]rive:"DR:DRIVEDR
75 GOSUB1055:A=INSTR("123DWX",A$):IFA=0THENSOUND200,1:GOTO75
80 ONA GOTO105,205,305,85,90,95
85 LOCATETB+24,3:PRINT"?";:GOSUB1055:A=INSTR("0123",A$):IFA$=CHR$(13)ORA=0THEN70ELSEDR=A-1:GOTO70
90 IFSW=40THENSW=80ELSESW=40
91 WIDTHSW:TB=(SW/40-1)*20:GOTO55
95 LOCATETB+5,23:PRINT"Exit Banners? [Y]es or [N]o :";:GOSUB1055:IFA$<>"Y"THEN55
96 CLS:PRINTTAB(TB+4)"Thank You for using MiniBanners!":ATTR1,1:PRINT:ATTR0,0:PRINT
97 POKE65496,0:POKE150,18:POKE282,255:POKE63395,64:POKE63468,64:POKE63503,64:POKE63567,64:POKE63644,64:POKE63771,64:NEW
100 REM * Print a Banner
105 PALETTE8,BG:CLS:PRINTTAB(TB+10)"/) Print a Banner (\":ATTR1,1:PRINT" ----!----1----!----2----!----3----!---":ATTR0,0:GOSUB880
110 LOCATETB+9,2:PRINTUSING"Printer Width [###] :";PW;:GOSUB1005:A=VAL(A$):IFA=0THENA=PW
111 IFA<32ORA>132THEN110ELSEPW=A
112 LOCATETB+9,3:PRINTUSING"Font Width    [###] :";WD;:GOSUB1005:A=VAL(A$):IFA>0ANDA<20THENWD=A
113 LOCATETB+9,4:PRINTUSING"Line Spacing  [###] :";SP;:GOSUB1005:IFA$=""THEN114ELSEA=VAL(A$):IFA=>0ANDA<20THENSP=A
114 LOCATETB+9,5:PRINTUSING"Ptr Character [###] :";CH;:GOSUB1005:IFA$=""THEN115ELSECH=VAL(A$)
115 B=0:CT=0:HT=INT(PW/(8+SP)):LN=0:MX=0:LOCATE0,2:PRINT:PRINT:PRINT:PRINT:ATTR1,1:PRINT"Ln Ht Text":ATTR0,0
120 LOCATE0,3:PRINT:LOCATE0,3:POKE282,0:PRINT">";:GOSUB1005:IFA$=""THENIFLN=0THEN55ELSELN=LN-1:GOTO140
121 IFA$=""THENA$=CHR$(127)
125 LN$(LN)=A$:IFLEN(A$)>MX THENMX=LEN(A$)
130 LOCATETB+11,4:PRINTUSING"Height [##] Max  :";HT;:GOSUB1005:A=VAL(A$):IFA=0THENA=HT
131 IFA<1ORA>HT THEN130ELSEHT(LN)=A:HT=HT-A:B=B+A*8+SP
135 LOCATE0,7+LN:PRINTUSING"## ";LN+1;HT(LN);:PRINTLEFT$(LN$(LN),33):IFHT>0THENLN=LN+1:GOTO120
140 CT=(PW-B)/2+SP/2:IFHT>0THENLOCATETB+6,23:PRINT"Center from Top to Bottom?";:GOSUB1055:IFA$<>"Y"THENCT=0
150 FORA=0TOLN:A$=STRING$(MX,32):IFLEN(LN$(A))<>MX THENMID$(A$,(MX-LEN(LN$(A)))/2+1,LEN(LN$(A)))=LN$(A):LN$(A)=A$
151 NEXT
155 LOCATETB+6,23:PRINT"PRINT - Is Everything Okay?";:GOSUB1055:IFA$<>"Y"THEN105
156 IF(PEEK(&HFF22)AND1)>0THENLOCATETB+6,23:ATTR1,1,B:PRINT" Ready Printer or Press [X]";:ATTR0,0:GOSUB1055:IFA$="X"THEN55ELSE156
160 LOCATETB+6,23:PRINT"PRINTING - Press [X] to Stop";:PRINT#DV:PRINT#DV,TAB(PW/2-15)"/) MiniBanners "VR$" SN#"SN$" (\"
165 PRINT#DV:FORA=1TOMX:FORB=7TO0STEP-1:FORW=1TOWD:PRINT#DV,STRING$(CT,32);:FORC=LN TO0STEP-1:LT=ASC(MID$(LN$(C),A,1))-32:FORD=7TO0STEP-1
170 IF(PEEK(&HF09D+LT*8+D)AND2^B)>0THENPRINT#DV,STRING$(HT(C),CH);ELSEPRINT#DV,STRING$(HT(C),32);
175 NEXT:IFLN>0ANDC>0THENPRINT#DV,STRING$(SP,32);
177 POKE282,255:IFINKEY$="X"THEN55
180 NEXT:PRINT#DV:NEXT:NEXT:NEXT:GOTO55
200 REM * Select Font
205 PALETTE8,BG:CLS:PRINTTAB(TB+12)"/) Select Font (\":ATTR1,1:PRINT:ATTR0,0:LOCATE0,22:ATTR1,1:PRINT:ATTR0,0
210 EX$="FNT":GOSUB930:IFA$=""THEN55
215 GOSUB840:IFA=0THEN210ELSEPOKE65496,0:LOADMA$+"."+EX$:POKE65497,0:FT$=A$
220 GOTO55
300 REM * Configuration
305 PALETTE8,BG:CLS:PRINTTAB(TB+10)"/) Configuration (\":ATTR1,1:PRINT:ATTR0,0:PRINT:LOCATE0,22:ATTR1,1:PRINT:ATTR0,0
310 LOCATETB+11,3:PRINT"Current Settings:":PRINT:PRINTTAB(TB)"Printer Width:"PW:PRINT:PRINTTAB(TB)"Printer Baud :"BD"-"PEEK(150):PRINT:PRINTTAB(TB)"Font Width   :"WD:PRINT:PRINTTAB(TB)"Line Spacing :"SP
311 PRINT:PRINTTAB(TB)"Banner Char. :"CH:PRINT:PRINTTAB(TB)"Default Font : "DF$
315 LOCATETB+2,23:PRINT"[E]dit [L]oad [S]ave or [X]it Config";:GOSUB1055:A=INSTR("ELSX",A$):IFA=0THEN315ELSEIFA=4THEN55
320 IFA=2THENGOSUB755:GOTO305ELSEIFA=3THENGOSUB780:GOTO305
325 LOCATETB+2,20:PRINT"Enter your printer's carraige width.":LOCATETB+15,5:GOSUB1005:A=VAL(A$):IFA>32ANDA<132THENPW=A
330 LOCATETB+14,5:PRINTPW:LOCATETB+2,20:PRINT"   Enter your printer's baud rate.":LOCATETB+15,7:GOSUB1005:A=VAL(A$):IFA>0THENBD=A:GOSUB880
335 LOCATETB+14,7:PRINTBD"-"PEEK(150):LOCATETB+2,20:PRINT"Enter default font width for banners.":LOCATETB+15,9:GOSUB1005:A=VAL(A$):IFA>0ANDA<10THENWD=A
340 LOCATETB+14,9:PRINTWD:LOCATETB+2,20:PRINT" Enter spacing between banner lines.":LOCATETB+15,11:GOSUB1005:A=VAL(A$):IFA$=""THEN345ELSEIFA=>0ANDA<20THENSP=A
345 LOCATETB+14,11:PRINTSP:LOCATETB+2,20:PRINT"Enter ASCII value of char. to print.":LOCATETB+15,13:GOSUB1005:A=VAL(A$):IFA>32THENCH=A
350 LOCATETB+14,13:PRINTCH:LOCATETB+2,20:PRINT" Enter font name to load on startup.":LOCATETB+15,15:POKE282,255:GOSUB1005:IFA$<>""THENDF$=LEFT$(A$,8)
395 GOTO305
500 PRINT"[END]";
501 GOTO501
700 REM * Break Detected
705 HSCREEN0:CLS:PRINTTAB(TB+10)"/) Break Detected (\":ATTR1,1:PRINT:ATTR0,0
710 LOCATETB+4,13:PRINT"[ENTER] to Restart, [X] to Exit:";:GOSUB1055:IFA$="*"THENPOKE65496,0:POKE282,255:STOP
715 IFA$="X"THEN95ELSERUN
720 END
725 REM * Error Detected :(
730 HSCREEN0:CLS:PRINTTAB(TB+10)"/) Error Detected (\":ATTR1,1:PRINT:ATTR0,0
735 LOCATETB+6,4:PRINT"[ Error"ERNO"reported at"ERLIN"]":PRINT:PRINTTAB(TB)" Honestly, I thought I had it bug-free!":PRINT:PRINTTAB(TB)"   If this was something other than a":PRINTTAB(TB)"   Disk Full or I/O Error, please let"
736 PRINTTAB(TB)"   us know so we may exterminate it."
740 GOTO710
750 REM * Load Configuration
755 A$="MINIBAN":EX$="CFG":GOSUB840:IFA=0THENRETURN
760 POKE65496,0:OPEN"I",#1,"MINIBAN.CFG":INPUT#1,CH,BD,PW,SP,WD,DF$,SW,FG,BG:CLOSE:POKE65497,0:FT$=DF$
765 A$=FT$:EX$="FNT":GOSUB840:IFA=0THENRETURNELSEPOKE65496,0:LOADMA$+"."+EX$:POKE65497,0
770 RETURN
775 REM * Save Configuration
780 POKE65496,0:OPEN"O",#1,"MINIBAN.CFG":WRITE#1,CH,BD,PW,SP,WD,DF$,SW,FG,BG:CLOSE:POKE65497,0:RETURN
800 REM * Directory Subroutine
805 GOSUB855:C=0:FORA=3TO11:POKE65496,0:DSKI$DR,17,A,A$,B$:POKE65497,0:C$=A$+LEFT$(B$,127):FORB=1TO255STEP32:IFEX$>""THENIFMID$(C$,B+8,3)<>EX$THEN820
810 D=ASC(MID$(C$,B,1)):IFD=0THEN815ELSEIFD=255THENRETURNELSEC=C+1:A$=MID$(C$,B,8):PRINT"    "A$"."MID$(C$,B+8,3)"    ";
815 D=20+(SGN(LEN(EX$))*10):D=D*(TB/20+1):IFC/D=INT(C/D)THEN825
820 NEXT:NEXT:RETURN
825 LOCATETB+2,23:PRINT"Press [ENTER] for More, [X] to Stop:";:GOSUB1055:LOCATETB+0,23:PRINTSTRING$(39,32);:IFA$="X"THENRETURN
830 GOSUB855:C=0:GOTO820
835 REM * Verify File Name
840 A=0:FORB=3TO11:POKE65496,0:DSKI$DR,17,B,C$,D$:POKE65497,0:D$=C$+LEFT$(D$,127):FORC=1TO255STEP32:IFMID$(D$,C,8)+"."+MID$(D$,C+8,3)=A$+STRING$(8-LEN(A$),32)+"."+EX$ THEN845 ELSENEXT:NEXT:RETURN
845 A=1:RETURN
850 REM * Clear Screen Sub
855 FORC=0TO15:LOCATE0,5+C:PRINT:NEXT:LOCATE0,6:RETURN
870 NEXT:NEXT:RETURN
875 REM * Baud Rate Routine
880 POKE150,INT(.2175+5.7825*2^(5-(LOG(BD/600)/LOG(2)))-4.5):RETURN
925 REM * Load from Disk...
930 LOCATETB+5,3:PRINT"Enter Font Name, [D]irectory,":PRINTTAB(TB+5)"or [P]review: ........":LOCATETB+19,4:POKE282,255:GOSUB1005:IFA$=""THENRETURNELSEIFLEN(A$)>8THEN930
935 IFA$="D"THENGOSUB805:GOTO930 ELSEIFA$="P"THENGOSUB950:GOTO930
940 RETURN
945 REM * Font Preview
950 HSCREEN2:HCOLOR8:LN$="        ":FORA=32TO127:LN$=LN$+CHR$(A):NEXT
955 C=0:FORA=3TO11:POKE65496,0:DSKI$DR,17,A,A$,B$:POKE65497,0:C$=A$+LEFT$(B$,127):FORB=1TO255STEP32:IFMID$(C$,B+8,3)<>"FNT"THEN970ELSED=ASC(MID$(C$,B,1)):IFD=0THEN965ELSEIFD=255THENRETURNELSEA$=MID$(C$,B,8):POKE65496,0:LOADMA$+".FNT":POKE65497,0
960 MID$(LN$,1,8)=A$:HPRINT(0,C*4),LEFT$(LN$,40):HPRINT(0,C*4+1),MID$(LN$,40,40):HPRINT(0,C*4+2),MID$(LN$,80):C=C+1
965 IFC/6=INT(C/6)THENGOSUB1055:IFA$="X"THENHSCREEN0:RETURNELSEHCLS:C=0
970 NEXT:NEXT:IFC>0THENGOSUB1055
971 HSCREEN0:RETURN
975 REM * Press Enter...
980 LOCATETB+7,23:PRINT"Press [ENTER] to Continue:";
985 IFINKEY$<>CHR$(13)THEN985ELSERETURN
998 PRINT:PRINT"*ERROR* "ERNO;ERLIN
999 WIDTH80:POKE65496,0:POKE150,18:END
1000 REM * Line Input
1005 PALETTE8,FG:LINEINPUTA$:RETURN
1050 REM * Inkey$
1055 PALETTE8,FG
1060 A$=INKEY$:IFA$=""THEN1060ELSESOUND1,1:LT=ASC(A$):IFLT>96THENA$=CHR$(LT-32)
1065 RETURN
5000 '
5001 'V0.0 - Program Began 9/20/90
5002 'V1.0 - CoCo Fest Release 10/90
5003 'V1.1 - 80 Col. & Updates 10/90
